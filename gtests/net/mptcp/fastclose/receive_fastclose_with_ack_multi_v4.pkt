// connection initiated by packetdrill
--tolerance_usecs=100000
`../common/defaults.sh`

+0    `../common/server.sh`

+0    socket(..., SOCK_STREAM, IPPROTO_MPTCP) = 3
+0    setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0

+0    bind(3, ..., ...) = 0
+0    listen(3, 1) = 0
+0.0    < addr[caddr0] > addr[saddr0] S 0:0(0) win 65535 <mss 1460,nop,wscale 7,mpcapable v1 flags[flag_h] nokey>
+0.0 > S. 0:0(0) ack 1 <mss 1460,nop,wscale 8,mpcapable v1 flags[flag_h] key[skey]>
+0.0 < . 1:1(0) ack 1 win 256 <mpcapable v1 flags[flag_h] key[ckey=2,skey]>
+0    accept(3, ..., ...) = 4

+0.0  < P. 1:1001(1000) ack 1 win 450 <nop, nop, dss dack8=1 dsn8=1 ssn=1 dll=1000 nocs>
+0.0  >  . 1:1(0) ack 1001 <dss dack8=1001 nocs>

+0.2 < P. 1:1(0) ack 1 win 450 <mpcapable v1 flags[flag_h] key[skey,ckey]>
+0.0 > . 1:1(0) ack 1001 <add_address addr[saddr1] hmac=auto,dss dack8=1001 dll=0 nocs>

// add another subflow.
+0.5 < addr[caddr1] > addr[saddr1] S 0:0(0) win 65535 <mss 1460,nop,wscale 7,mp_join_syn address_id=2 token=sha256_32(skey)>
+0.0 > S. 0:0(0) ack 1 <mss 1460,nop,wscale 8,mp_join_syn_ack address_id=0 sender_hmac=auto>
+0.0 < . 1:1(0) ack 1 win 256 <mp_join_ack sender_hmac=auto>
+0.0 > . 1:1(0) ack 1 <dss dack8=1001 nocs>

// send more data at mptcp level.
+0.2  < P. 1:1002(1001) ack 1 win 450 <nop, nop, dss dack8=1 dsn8=1001 ssn=1 dll=1001 nocs>
+0.0  >  . 1:1(0) ack 1002 <dss dack8=2002 dll=0 nocs>

// fastclose on the 2nd subflow, but old sequence numbers.
+0.1  <  . 1:1(0) win 450 <mp_fastclose>

// no effect expected.
+0.0  >  . 1:1(0) ack 1002 <dss dack8=2002 dll=0 nocs>

// fastclose on the 2nd subflow, good sequence numbers.
+0.1  <  . 1002:1002(0) win 450 <mp_fastclose>

// expect peer to reset both subflows now:
+0.0  > addr[saddr0] > addr[caddr0] R. 1:1(0) ack 1001
+0.0  > addr[saddr1] > addr[caddr1] R. 1:1(0) ack 1002

+0    read(4, ..., 2002) = 2001
+0    close(4) = 0
